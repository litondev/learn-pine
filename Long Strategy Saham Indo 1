//@version=5
strategy("Long Strategy Saham Indo", overlay=true,
  initial_capital=1000, 
  commission_type=strategy.commission.percent, commission_value=0.00,
  process_orders_on_close = true, calc_on_every_tick = false)

// ========== INPUT ==========
demaFastLen = input.int(20, "DEMA Fast (Trend)")
demaSlowLen = input.int(50, "DEMA Slow (Trend)")

useSL       = input.bool(true, "Enable Stop Loss")
riskPct     = input.float(100, "Risk Allocation (%)", minval=0.1)  
leverage    = input.int(1, "Leverage (X)", minval=1)          
slPctOfRisk = input.float(15, "Stop Loss (% dari Risk Allocation)", minval=0.1)

// ========== DEMA ==========
ema_fast  = ta.ema(close, demaFastLen)
ema_fast2 = ta.ema(ema_fast, demaFastLen)
dema20    = 2 * ema_fast - ema_fast2

ema_slow  = ta.ema(close, demaSlowLen)
ema_slow2 = ta.ema(ema_slow, demaSlowLen)
dema50    = 2 * ema_slow - ema_slow2

// ========== INDICATORS & SIGNALS ==========
// DEMA Cross
bullCross   = ta.crossover(dema20, dema50)
bearCross   = ta.crossunder(dema20, dema50)

// ========== ORDER SIZE (riskPct * leverage) ==========
accountEquity = strategy.equity           
riskCapital   = accountEquity * (riskPct / 100)
positionValue = riskCapital * leverage
qtyContracts  = positionValue / close

// ========== EXECUTE TRADE ==========
varip isBull = true     

if bullCross
    isBull := true
 
if bearCross
    isBull := false

buyId = "BUY Entry"

if (isBull and close > dema50 and strategy.position_size == 0)
    strategy.entry(buyId, strategy.long, qty=qtyContracts)

// ========== EXIT LOGIC ==========
if (strategy.position_size > 0)
    entryPrice = strategy.position_avg_price

    if useSL 
        lossAllowed = riskCapital * (slPctOfRisk / 100)
        stopMove    = lossAllowed / qtyContracts
        stopPrice   = entryPrice - stopMove
        strategy.exit("Exit SL", from_entry=buyId, stop=stopPrice)

    if bearCross
        strategy.close(buyId, comment="Exit by DEMA weekly cross down")

// ========== PLOT ==========
plot(dema20, title="DEMA20", color=color.yellow, linewidth=3)
plot(dema50, title="DEMA50", color=color.green, linewidth=3)

plotshape(series=bullCross, title="Sinyal Beli", location=location.belowbar, color=color.new(color.green, 0), style=shape.triangleup, text="BUY", size=size.small)
plotshape(series=bearCross, title="Sinyal Jual", location=location.abovebar, color=color.new(color.red, 0), style=shape.triangledown, text="SELL", size=size.small)
